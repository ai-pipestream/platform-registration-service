pluginManagement {
    repositories {
        // Allow locally published Pipestream plugins (e.g., proto-toolchain SNAPSHOTs)
        mavenLocal {
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        // GitHub Packages (optional) for Pipestream plugins if credentials are provided
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
            .orElse(providers.gradleProperty("gpr.key"))
            .orElse(providers.environmentVariable("GPR_TOKEN"))

        def githubActor = providers.environmentVariable("GITHUB_ACTOR")
            .orElse(providers.gradleProperty("gpr.user"))
            .orElse(providers.environmentVariable("GPR_USER"))

        if (githubToken.isPresent() && githubActor.isPresent()) {
            maven {
                url = uri("https://maven.pkg.github.com/ai-pipestream/bom")
                credentials {
                    username = githubActor.get()
                    password = githubToken.get()
                }
                content {
                    includeGroupByRegex "ai\\.pipestream(\\..*)?"
                }
            }
        }

        gradlePluginPortal()
    }
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == "ai.pipestream.proto-toolchain") {
                useVersion("0.7.14")
            }
        }
    }
}

rootProject.name = 'platform-registration-service'

dependencyResolutionManagement {
//    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)

    versionCatalogs {
        libs {
            // Import version catalog from published BOM
            // Available on Maven Central and GitHub Packages
            from("ai.pipestream:pipestream-bom-catalog:${pipestreamBomVersion}")
        }
    }

    repositories {
        // Maven Local for published Pipeline artifacts during development
        mavenLocal {
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        // Maven Central (always available, default for public builds)
        mavenCentral()

        // Maven Central Snapshots for consuming published SNAPSHOTs
        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
        }

        // Apache snapshots for Tika 4.x pre-release (default for public builds)
        if (System.getenv('CI_ENV') != 'internal') {
            maven {
                url = uri('https://repository.apache.org/content/repositories/snapshots/')
                allowInsecureProtocol = false
                content {
                    includeGroupByRegex "org\\.apache\\.tika(\\..*)?"
                }
            }
        }
    }
}
